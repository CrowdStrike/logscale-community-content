name: urlFalconPid
visualization:
  options:
    columns: '[{"type":"field","fieldName":"@timestamp","format":"timestamp","width":180},{"type":"field","fieldName":"@rawstring","format":"json"}]'
    newestAtBottom: 'true'
    showOnlyFirstLine: 'false'
  type: list-view
$schema: https://schemas.humio.com/query/v0.1.0
timeInterval:
  isLive: false
  start: 1d
queryString: |-
  // Returns CrowdStrike-specific URLs that utilize the "pid" field.
  //
  // To call this function if installed from the crowdstrike/fltr-run package:
  // $"crowdstrike/fltr-run:urlFalconPid"()
  //
  // To call this function in a query if you have it saved locally:
  // $"urlFalconPid"()

  // By default we ignore anything older than 7 days for Graph Explorer and Process Explorer. 
  | ignoreTime:=604800

  // Parse values from the ConfigBuild to determine the URL.
  | ConfigBuild=/(?<config>\d+)\.(?<opSystem>\d+)\.(?<falconBuild>\d+)\.(?<cloudDecimal>\d+$)/i

  // Figure out the URL from those parsed values. 
  | cloudDecimal match {
      1 => rootURL  := "https://falcon.crowdstrike.com/" ;
      9 => rootURL  := "https://falcon.laggar.gcw.crowdstrike.com/" ;
      10 => rootURL := "https://falcon.eu-1.crowdstrike.com/" ;
      11 => rootURL := "https://falcon.us-2.crowdstrike.com/" ;
    }

  // Make writing the URL a bit easier. 
  | colon := "%3A"
  | tick := "%27"
  | plus := "%2B"

  // The case statements will only return URLs where it only has the necessary fields.

  // Check to make sure the event is < 7 days ago, otherwise assign an "PID > 7 Days" value. 
  | case {
      aid=* TargetProcessId=* | test(ProcessStartTime < (start()/1000 - ignoreTime))
        // Event is outside the 7-day retention for Process Explorer. 
        | format("[PID > 7 Days](%s)", field=["rootURL"], as="Process Explorer")
        // Event is outside the 7-day retention for Graph Explorer.       
        | format("[PID > 7 Days](%s)", field=["rootURL"], as="Graph Explorer") ;
      aid=* TargetProcessId=*
        // Process Explorer. 
        | format("[Process Explorer](%sinvestigate/process-explorer/%s/%s)", field=["rootURL", "aid", "TargetProcessId"], as="Process Explorer")
        // Graph Explorer.
        | format("[Graph Explorer](%sgraphs/process-explorer/graph?id=pid:%s:%s)", field=["rootURL", "aid", "TargetProcessId"], as="Graph Explorer") ;
      * ;
    }

  // Drop the fields that are no longer needed. 
  | drop([colon, tick, plus, rootURL, config, opSystem, falconBuild, cloudDecimal, thisStartTime, ignoreTime])
