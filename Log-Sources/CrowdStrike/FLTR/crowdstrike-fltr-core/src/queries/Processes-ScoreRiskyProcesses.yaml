name: Processes - Score Risky Processes
visualization:
  options: {}
  type: table-view
$schema: https://schemas.humio.com/query/v0.1.0
timeInterval:
  isLive: false
  start: 7d
queryString: |-
  // Get all Windows ProcessRollup2 Events.
  #event_simpleName=ProcessRollup2 event_platform=Win

  // Narrow to processes of interest and create FileName variable.
  | ImageFileName=/\\(?<FileName>(whoami|net1?|systeminfo|ping|nltest|sc)\.exe)/i

  // Get timestamp value with date and hour value.
  | ProcessStartTime:=ProcessStartTime*1000
  | dayBucket:=formatTime("%Y-%m-%d %H", field=ProcessStartTime, locale=en_US, timezone=Z)

  // Parse flag used in "net" command.
  | regex("(sc|net1?)\s+(?<netFlag>\S+)\s+", field=CommandLine, strict=false, flags=i)

  // Create evaulation criteria and weighting for process usage.
  | default(field=behaviorWeight, value=0)
  | case {
      FileName=/net1?\.exe/i netFlag="start"                              | behaviorWeight := "4" ;
      FileName=/net1?\.exe/i netFlag="stop"                               | behaviorWeight := "4" ;
      FileName=/net1?\.exe/i netFlag="stop" CommandLine=/falcon/i         | behaviorWeight := "25" ;
      FileName=/sc\.exe/i netFlag="start"                                 | behaviorWeight := "4" ;
      FileName=/sc\.exe/i netFlag="stop"                                  | behaviorWeight := "4" ;
      FileName=/sc\.exe/i netFlag=/(query|stop)/i CommandLine=/csagent/i  | behaviorWeight := "25" ;
      FileName=/net1?\.exe/i netFlag="share"                              | behaviorWeight := "2" ;
      FileName=/net1?\.exe/i netFlag="user" CommandLine=/\/delete/i       | behaviorWeight := "10" ;
      FileName=/net1?\.exe/i netFlag="user" CommandLine=/\/add/i          | behaviorWeight := "10" ;
      FileName=/net1?\.exe/i netFlag="group" CommandLine=/\/domain\s+/i   | behaviorWeight := "5" ;
      FileName=/net1?\.exe/i netFlag="group" CommandLine=/\/admin/i       | behaviorWeight := "5" ;
      FileName=/net1?\.exe/i netFlag="localgroup" CommandLine=/\/add/i    | behaviorWeight := "10" ;
      FileName=/net1?\.exe/i netFlag="localgroup" CommandLine=/\/delete/i | behaviorWeight := "10" ;
      FileName=/nltest\.exe/i                                             | behaviorWeight := "3" ;
      FileName=/systeminfo\.exe/i                                         | behaviorWeight := "3" ;
      FileName=/whoami\.exe/i                                             | behaviorWeight := "3" ;
      FileName=/ping\.exe/i                                               | behaviorWeight := "3" ;
      * ;
    }

  // Create FileName and CommandLine one-liner.
  | format(format="Score: %s\t|\t%s -> %s", field=[behaviorWeight, FileName, CommandLine], as="executionDetails")

  // Group and organize output.
  | groupby([cid, aid, dayBucket], function=[count(FileName, distinct=true, as="fileCount"), sum(behaviorWeight, as="totalScore"), collect(executionDetails)], limit=max)

  // Add the ComputerName
  | $"crowdstrike/fltr-run:ComputerName"()

  // Set thresholds.
  | fileCount >= 5 OR totalScore > 30
  | sort(totalScore)

  // Format the final output.
  | "Total Score":=rename(totalScore)
  | "Process Details":=rename(executionDetails)
  | table([aid, ComputerName, "Total Score", "Process Details"], limit=1000)
